def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.google.gms.google-services'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def credentialsProperties = new Properties()
def credentialsPropertiesFile = rootProject.file('credentials.properties')
if (credentialsPropertiesFile.exists()) {
    credentialsProperties.load(new FileInputStream(credentialsPropertiesFile))
}

android {
    compileSdkVersion 33
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).

        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion 23
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "hepyapp"
    productFlavors{
        dev{
            dimension "hepyapp"
            applicationId "com.mandaliyas.hepyapp.dev"
            resValue "string", "app_name", "Hepy Dev"
            resValue "string", "facebook_app_id", credentialsProperties['dev.facebook_app_id']
            resValue "string", "fb_login_protocol_scheme", credentialsProperties['dev.fb_login_protocol_scheme']
            resValue "string", "facebook_client_token", credentialsProperties['dev.facebook_client_token']
        }
        prod {
            dimension "hepyapp"
            applicationId "com.hepy.app"
            resValue "string", "app_name", "Hepy"
            resValue "string", "facebook_app_id", credentialsProperties['prod.facebook_app_id']
            resValue "string", "fb_login_protocol_scheme", credentialsProperties['prod.fb_login_protocol_scheme']
            resValue "string", "facebook_client_token", credentialsProperties['prod.facebook_client_token']
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation platform('com.google.firebase:firebase-bom:30.2.0')
    implementation 'com.facebook.android:facebook-android-sdk:14.1.0'
    implementation 'com.google.firebase:firebase-analytics'
    implementation "androidx.browser:browser:1.4.0"
}
